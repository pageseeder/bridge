/**
 * Common publishing plugin
 */
publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      pom {
        // We need to so this to ensure that subprojects get the correct title/description
        afterEvaluate {
          name = "${project.title}"
          description = "${project.description}"
        }
        url = "$website"

        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        organization {
          name = 'Allette Systems'
          url = 'https://www.allette.com.au'
        }

        scm {
          url = "git@github.com:pageseeder/${gitName}.git"
          connection = "scm:git:git@github.com:pageseeder/${gitName}.git"
          developerConnection = "scm:git:git@github.com:pageseeder/${gitName}.git"
        }
      }
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

  // Reference to Maven config
  publications = ['mavenJava']

  publish = true

  pkg {
    repo = 'maven'
    name = "${gitName}"
    userOrg = 'pageseeder'
    desc = "$description"
    websiteUrl = "$website"
    vcsUrl = "https://github.com/pageseeder/${gitName}.git"
    licenses = ['Apache-2.0']
    labels = "$labels".split(',')
    githubRepo = "pageseeder/${gitName}"
    githubReleaseNotesFile = 'README.md'

    version {
      name = "${project.version}"
    }
  }

}

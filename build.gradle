plugins {
  id 'java-library'
  id 'maven-publish'
  id 'io.codearte.nexus-staging' version '0.30.0'
}

ext {
  globalVersion = file('version.txt').text
}

allprojects {
  group   = 'org.pageseeder.bridge'
  version = "$globalVersion"

  apply plugin: 'java-library'
  apply from: "$rootDir/gradle/publish-mavencentral.gradle"

  java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  repositories {
    mavenCentral {
      url = uri("https://maven-central.storage.googleapis.com/maven2")
    }

    // Takes our libraries immediately as the maven central should take hours to get synchronized.
    maven { url 'https://s01.oss.sonatype.org/content/groups/public/' }
  }

  jar {
    manifest {
      attributes 'Implementation-Vendor':  'Allette Systems',
                 'Implementation-Title':   "$title",
                 'Implementation-Version': "$globalVersion"
    }
  }

  javadoc {

    // Let's package the javadoc even if there are some lint issues
    options.addStringOption('Xdoclint:none', '-quiet')

    // Generate better doc if we can
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }

}

dependencies {

  api('org.pageseeder.xmlwriter:pso-xmlwriter:1.0.3')
  api('net.sf.ehcache:ehcache:2.8.8')
  api('javax.cache:cache-api:1.0.0')

  implementation('org.slf4j:slf4j-api:1.7.30')

  compileOnly ('org.eclipse.jdt:org.eclipse.jdt.annotation:2.0.0')

  testImplementation('junit:junit:4.12')
  testImplementation('org.slf4j:slf4j-simple:1.7.30')

}

wrapper {
  gradleVersion = '7.0'
  distributionType = Wrapper.DistributionType.ALL
}

plugins {
  id 'java-library'
  id 'maven-publish'
  id 'io.codearte.nexus-staging' version '0.30.0'
}

ext {
  globalVersion = file('version.txt').text
}

allprojects {
  group   = 'org.pageseeder.bridge'
  version = "$globalVersion"

  apply plugin: 'java-library'
  apply from: "$rootDir/gradle/publish-mavencentral.gradle"

  java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  repositories {
    mavenCentral {
      url = uri("https://maven-central.storage.googleapis.com/maven2")
    }

    // Takes our libraries immediately as the maven central should take hours to get synchronized.
    maven { url 'https://s01.oss.sonatype.org/content/groups/public/' }
  }

  jar {
    manifest {
      attributes 'Implementation-Vendor':  'Allette Systems',
                 'Implementation-Title':   "$title",
                 'Implementation-Version': "$globalVersion"
    }
  }

  javadoc {

    // Let's package the javadoc even if there are some lint issues
    options.addStringOption('Xdoclint:none', '-quiet')

    // Generate better doc if we can
    if (JavaVersion.current().isJava9Compatible()) {
      options.addBooleanOption('html5', true)
    }
  }

}

dependencies {

  api(libs.xmlwriter)
  api(libs.ecache)
  api(libs.cache.api)
  //JDK 11 does not java this module (xml.bind) http://openjdk.java.net/jeps/320
  api(libs.jakarta.xml.bind)

  implementation(libs.slf4j.api)

  compileOnly (libs.annotations)

  testImplementation(libs.junit)
  testImplementation(libs.slf4j.simple)

}

wrapper {
  gradleVersion = '8.14.4'
  distributionType = Wrapper.DistributionType.ALL
}
